17 22
META script_type_update
META script_execution_entry

/// Commonly used values
STR message = ""
STR username = ""
INT index = 0
INT maxIndex = 0

/// Notify all other players that a player joined the game
STR newUsername = fe3d:server_get_new_username()
IF newUsername NOT "":
    EDIT message = misc:string_concat("player_join:", newUsername)
    fe3d:server_broadcast_tcp_message(message, newUsername)

/// Notify all other players that a player left the game
STR oldUsername = fe3d:server_get_old_username()
IF oldUsername NOT "":
    EDIT message = misc:string_concat("player_leave:", oldUsername)
    fe3d:server_broadcast_tcp_message(message, oldUsername)

/// Send all connected usernames to the joined player
IF newUsername NOT "":
    LIST connectedUsernames = fe3d:server_get_connected_usernames()
    EDIT index = 0
    EDIT maxIndex = misc:list_get_size("connectedUsernames")
    LOOP:
        IF index IS maxIndex:
            BREAK
        ELIF connectedUsernames[index] NOT newUsername:
            EDIT message = misc:string_concat("player_join:", connectedUsernames[index])
            fe3d:server_send_tcp_message(newUsername, message)
        INCR index 1

/// Extract messages
LIST messageUsernames = fe3d:server_get_pending_usernames()
LIST messageContents = fe3d:server_get_pending_messages()
LIST stringParts = {}
STR log = ""
EDIT index = 0
EDIT maxIndex = misc:list_get_size("messageContents")
BOOL contains = <false>
BOOL isForwardTCP = <false>
BOOL isForwardUDP = <false>
LOOP:
    IF index IS maxIndex:
        BREAK
    ELSE:
        EDIT username = messageUsernames[index]
        EDIT message = messageContents[index]
        EDIT contains = misc:string_contains(message, ":")
        IF contains IS <true>:
            EDIT stringParts = misc:string_split(message, ":")
            EDIT isForwardTCP = misc:list_contains("_forwardTCPs", stringParts[0])
            EDIT isForwardUDP = misc:list_contains("_forwardUDPs", stringParts[0])
            IF isForwardTCP IS <true>:
                fe3d:server_broadcast_tcp_message(message, username)
            ELIF isForwardUDP IS <true>:
                fe3d:server_broadcast_udp_message(message, username)
            ELSE:
                EDIT log = misc:string_concat("Received unknown message: ", message)
                fe3d:print(log)
                fe3d:game_stop()
                BREAK
        ELSE:
            EDIT log = misc:string_concat("Received invalid message: ", message)
            fe3d:print(log)
            fe3d:game_stop()
            BREAK
        INCR index 1
