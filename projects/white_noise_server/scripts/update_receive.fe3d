9 12
META script_type_update
META script_execution_waiting

/// Constants
CONST LIST messageUsernames = fe3d:server_get_pending_usernames()
CONST LIST messageProtocols = fe3d:server_get_pending_protocols()
CONST LIST messageContents = fe3d:server_get_pending_contents()

/// Non-constants
LIST contentParts = {}
STR log = ""
STR username = ""
STR protocol = ""
STR content = ""
INT index = 0
INT maxIndex = misc:list_get_size("messageContents")
BOOL contains = <false>
BOOL mustForward = <false>
BOOL mustSave = <false>

/// Receive network messages
LOOP:
    IF index IS maxIndex:
        BREAK
    /// Extract message data
    EDIT username = messageUsernames[index]
    EDIT protocol = messageProtocols[index]
    EDIT content = messageContents[index]
    /// Check if message is valid
    EDIT contains = misc:string_contains(content, ":")
    IF contains IS <true>:
        EDIT contentParts = misc:string_split(content, ":")
        EDIT mustForward = misc:list_contains("_forwardMessageTypes", contentParts[0])
        EDIT mustSave = misc:list_contains("_saveMessageTypes", contentParts[0])
        /// Check if message needs to be forwarded
        IF mustForward IS <true>:
            IF protocol IS "TCP":
                fe3d:server_broadcast_tcp_message(content, username)
            ELSE:
                fe3d:server_broadcast_udp_message(content, username)
        /// Check if message needs to be saved
        IF mustSave IS <true>:
            IF mustForward IS <true>:
                PUSH _savedUsernames ""
            ELSE:
                PUSH _savedUsernames username
            PUSH _savedProtocols protocol
            PUSH _savedContents content
        /// Check if message is unknown
        IF mustForward IS <false> AND mustSave IS <false>:
            EDIT log = misc:string_concat("Received unknown message: ", content)
            fe3d:print(log)
            fe3d:application_stop()
    ELSE:
        EDIT log = misc:string_concat("Received invalid message: ", message)
        fe3d:print(log)
        fe3d:application_stop()
    INCR index 1
