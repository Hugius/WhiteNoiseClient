70 61
META script_type_update
META script_execution_waiting

/// Inputs
BOOL rPressed = fe3d:input_is_key_pressed("KEY_R")
BOOL lmbPressed = fe3d:input_is_mouse_pressed("BUTTON_LEFT")
BOOL lmbDown = fe3d:input_is_mouse_down("BUTTON_LEFT")
BOOL rmbPressed = fe3d:input_is_mouse_pressed("BUTTON_RIGHT")

/// Values
STR gunID = _primaryGunID
INT gunIndex = _primaryGunIndex
STR adsID = misc:concat_strings("skeleton_", gunID)
EDIT adsID = misc:concat_strings(adsID, "_ads")
STR gunShootID = misc:concat_strings(gunID, "_shoot")
STR gunReloadID = misc:concat_strings(gunID, "_reload")
STR armReloadID = misc:concat_strings("skeleton_", gunID)
EDIT armReloadID = misc:concat_strings(armReloadID, "_rld")

/// Update ADS - toggling
BOOL isAdsPlaying = <false>
BOOL isAdsStarted = fe3d:model_is_animation_started("player", adsID)
IF isAdsStarted  IS <true>:
    EDIT isAdsPlaying = fe3d:model_is_animation_playing("player", adsID)
IF rmbPressed IS <true> AND isAdsPlaying IS <false>:
    IF _isReloading IS <false>:
        IF _isADS IS <true>:
            fe3d:model_resume_animation("player", adsID)
            EDIT _isADS = <false>
            EDIT _targetYawOffset = _defaultYawOffset
        ELSE:
            fe3d:model_start_animation("player", adsID, 0)
            EDIT _isADS = <true>
            EDIT _targetYawOffset = 0.0

/// Update ADS - aiming animation
IF _yawOffset LESS _targetYawOffset:
    INCR _yawOffset _yawOffsetSpeed
ELIF _yawOffset MORE _targetYawOffset:
    DECR _yawOffset _yawOffsetSpeed

/// Update ADS - crosshair & FOV
DEC fov = fe3d:camera_get_fov()
IF _isADS IS <true>:
    IF fov MORE _adsFov:
        DECR fov _fovSpeed
    ELSE:
        EDIT fov = _adsFov
    fe3d:image_set_visible("crosshair", <false>)
ELSE:
    IF fov LESS _defaultFov:
        INCR fov _fovSpeed
    ELSE:
        EDIT fov = _defaultFov
    fe3d:image_set_visible("crosshair", <true>)
fe3d:camera_set_fov(fov)

/// Update ADS - arm animation
IF _isADS IS <true>:
    BOOL isArmStarted = fe3d:model_is_animation_started("player", adsID)
    IF isArmStarted IS <true>:
        BOOL isPlaying = fe3d:model_is_animation_playing("player", adsID)
        INT index = fe3d:model_get_animation_frame_index("player", adsID)
        IF isPlaying IS <true> AND index IS 2:
            fe3d:model_pause_animation("player", adsID)

/// Update shooting
BOOL isSemi = _gunDataSemi[gunIndex]
BOOL isShootStarted = fe3d:model_is_animation_started(gunID, gunShootID)
IF isShootStarted IS <false> AND _isReloading IS <false>:
    IF _isGunEquipped IS <true> AND _isFreeLooking IS <false>:
        IF isSemi IS <true>:
            IF lmbPressed IS <true>:
                fe3d:model_start_animation(gunID, gunShootID, 0)
                fe3d:sound_play_forced(gunShootID, 0, 0)
        ELSE:
            IF lmbDown IS <true>:
                fe3d:model_start_animation(gunID, gunShootID, 0)
                fe3d:sound_play_forced(gunShootID, 0, 0)

/// Update reloading - starting animation
IF rPressed IS <true> AND _isADS IS <false> AND _isReloading IS <false>:
    EDIT _isReloading = <true>
    fe3d:model_start_animation(gunID, gunReloadID, 0)
    fe3d:model_start_animation("player", armReloadID, 0)

/// Update reloading - cleanup
BOOL isReloadStarted = fe3d:model_is_animation_started("player", armReloadID)
IF _isReloading IS <true> AND isReloadStarted IS <false>:
    EDIT _isReloading = <false>
