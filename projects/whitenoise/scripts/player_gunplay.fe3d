53 10
META script_type_update
META script_execution_waiting

/// Inputs
BOOL rPressed = fe3d:input_is_key_pressed("KEY_R")
BOOL lmbPressed = fe3d:input_is_mouse_pressed("BUTTON_LEFT")
BOOL lmbDown = fe3d:input_is_mouse_down("BUTTON_LEFT")
BOOL rmbPressed = fe3d:input_is_mouse_pressed("BUTTON_RIGHT")

/// Update ADS - toggle
IF rmbPressed IS <true>:
    IF _isADS IS <true>:
        fe3d:model_resume_animation("player", "skeleton_m1911_ads")
        EDIT _isADS = <false>
    ELSE:
        fe3d:model_start_animation("player", "skeleton_m1911_ads", 0)
        EDIT _isADS = <true>
    EDIT _targetYawOffset = 0.0

/// Update ADS - gun animation
IF _yawOffset LESS _targetYawOffset:
    INCR _yawOffset 1.0
ELIF _yawOffset MORE _targetYawOffset:
    DECR _yawOffset 1.0

/// Update ADS - miscellaneous
DEC fov = fe3d:camera_get_fov()
IF _isADS IS <true>:
    IF fov MORE 60.0:
        DECR fov 0.5
    fe3d:image_set_visible("crosshair", <false>)
ELSE:
    EDIT _targetYawOffset = 24.0
    IF fov LESS 80.0:
        INCR fov 0.5
    fe3d:image_set_visible("crosshair", <true>)
fe3d:camera_set_fov(fov)

/// Update ADS - arm animation
IF _isADS IS <true>:
    STR animID = "skeleton_m1911_ads"
    BOOL isStarted = fe3d:model_is_animation_started("player", animID)
    IF isStarted IS <true>:
        BOOL isPlaying = fe3d:model_is_animation_playing("player", animID)
        INT index = fe3d:model_get_animation_frame_index("player", animID)
        IF isPlaying IS <true> AND index IS 2:
            fe3d:model_pause_animation("player", animID)

/// Update shooting
BOOL isSemi = <true>
BOOL shootStarted = fe3d:model_is_animation_started("m1911", "m1911_shoot")
IF shootStarted IS <false>:
    IF isSemi IS <true>:
        IF lmbPressed IS <true>:
            fe3d:model_start_animation("m1911", "m1911_shoot", 0)
    ELSE:
        if lmbDown IS <true>:
            fe3d:model_start_animation("m1911", "m1911_shoot", 0)

/// Update reloading
IF rPressed IS <true>:
    fe3d:model_start_animation("m1911", "m1911_reload", 0)
