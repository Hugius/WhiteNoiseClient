67 30
META script_type_update
META script_execution_waiting

/// Values
STR gunID = _primaryGunID
STR armEquipID = misc:concat_strings("skeleton_", gunID)
EDIT armEquipID = misc:concat_strings(armEquipID, "_eqp")

/// Update gun equipping - starting arm animation
IF _isGunEquipped IS <false> AND _isEquippingGun IS <false>:
    IF _isUnequippingGun IS <false>:
        fe3d:model_start_animation("player", armEquipID, 0)
        fe3d:model_set_visible(gunID, <true>)
        EDIT _isEquippingGun = <true>

/// Update gun equipping - pausing arm animation
IF _isEquippingGun IS <true>:
    BOOL isStarted = fe3d:model_is_animation_started("player", armEquipID)
    IF isStarted IS <true>:
        BOOL isPlaying = fe3d:model_is_animation_playing("player", armEquipID)
        INT index = fe3d:model_get_animation_frame_index("player", armEquipID)
        IF isPlaying IS <true> AND index IS 2:
            fe3d:model_pause_animation("player", armEquipID)
            EDIT _gunHeightOffset = 0.0
            EDIT _isEquippingGun = <false>
            EDIT _isGunEquipped = <true>

/// Update gun unequipping - finishing up
IF _isUnequippingGun IS <true>:
    BOOL isStarted = fe3d:model_is_animation_started("player", armEquipID)
    IF isStarted IS <false>:
        fe3d:model_set_visible(gunID, <false>)
        EDIT _gunHeightOffset = -1.0
        EDIT _isUnequippingGun = <false>
        EDIT _isGunEquipped = <false>

/// Update gun equipping - gun height
IF _isEquippingGun IS <true>:
    INCR _gunHeightOffset _gunHeightOffsetSpeed

/// Update gun unequipping - gun height
IF _isUnequippingGun IS <true>:
    DECR _gunHeightOffset _gunHeightOffsetSpeed

/// Update gun swapping - resuming arm animation
BOOL isArmsPlaying = fe3d:model_is_animation_started("player", "skeleton_walk_arms")
INT scrollDir = fe3d:input_get_mousewheel_direction()
IF scrollDir NOT 0 AND _isADS IS <false> AND _isReloading IS <false>:
    IF _isEquippingGun IS <false> AND _isUnequippingGun IS <false>:
        IF isArmsPlaying IS <false> AND _secondaryGunID NOT "":
            IF _isSwappingGun IS <false>:
                fe3d:model_resume_animation("player", armEquipID)
                EDIT _isGunEquipped = <false>
                EDIT _isUnequippingGun = <true>
                EDIT _isSwappingGun = <true>

/// Update gun swapping - finishing up
IF _isSwappingGun IS <true> AND _isUnequippingGun IS <false>:
    STR secondaryID = _secondaryGunID
    INT secondaryIndex = _secondaryGunIndex
    EDIT _secondaryGunID = _primaryGunID
    EDIT _secondaryGunIndex = _primaryGunIndex
    EDIT _primaryGunID = secondaryID
    EDIT _primaryGunIndex = secondaryIndex
    EDIT _isSwappingGun = <false>

/// Update camera lock
BOOL fDown = fe3d:input_is_key_down("KEY_F")
EDIT _isFreeLooking = fDown
IF _isFreeLooking IS <true>:
    fe3d:camera_unlock_pitch()
ELSE:
    fe3d:camera_lock_pitch()
    DEC pitch = fe3d:camera_get_pitch()
    DEC absPitch = math:abs(pitch)
    DEC negSpeed = _pitchSpeed
    NEG negSpeed
    IF absPitch LESS 0.000001:
        EDIT pitch = 0.0
    ELIF pitch MORE _pitchSpeed:
        DECR pitch _pitchSpeed
    ELIF pitch LESS negSpeed:
        INCR pitch _pitchSpeed
    fe3d:camera_set_pitch(pitch)
