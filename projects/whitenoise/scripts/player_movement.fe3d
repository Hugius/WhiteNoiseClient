43 32
META script_type_update
META script_execution_waiting

/// Revert back eye offset
DEC offset = _skeletonEyeOffset
NEG offset
fe3d:camera_follow_z(offset)

/// Movement input
BOOL wKeyDown = fe3d:input_is_key_down("KEY_W")
BOOL aKeyDown = fe3d:input_is_key_down("KEY_A")
BOOL sKeyDown = fe3d:input_is_key_down("KEY_S")
BOOL dKeyDown = fe3d:input_is_key_down("KEY_D")
BOOL shiftKeyDown = fe3d:input_is_key_down("KEY_LSHIFT")
BOOL isMoving = <false>
DEC speed = _playerMovementSpeed
DEC yaw = fe3d:camera_get_yaw()
INCR yaw _yawOffset

IF shiftKeyDown IS <true>:
    MUL speed _sprintMultiplier

IF wKeyDown IS <true>:
    fe3d:camera_follow_z(speed)
    EDIT isMoving = <true>
IF sKeyDown IS <true>:
    NEG speed
    fe3d:camera_follow_z(speed)
    EDIT isMoving = <true>
    NEG speed
IF dKeyDown IS <true>:
    fe3d:camera_follow_x(speed)
    EDIT isMoving = <true>
IF aKeyDown IS <true>:
    NEG speed
    fe3d:camera_follow_x(speed)
    EDIT isMoving = <true>
    NEG speed

VEC3 camPos = fe3d:camera_get_position()
VEC3 gunPos = camPos
VEC3 skeletonPos = camPos
VEC3 skeletonOffset = [-0.2 -3.2 -0.2]
VEC3 gunOffset = [1.1 -0.375 1.1]
DEC cosYaw = math:cos(yaw)
DEC sinYaw = math:sin(yaw)
MUL gunOffset.x cosYaw
MUL gunOffset.z sinYaw
INCR gunPos gunOffset
INCR skeletonPos skeletonOffset
///fe3d:model_set_position("player", skeletonPos.x, skeletonPos.y, skeletonPos.z)

VEC3 gunRot = fe3d:model_get_rotation("m1911")
EDIT gunRot.y = fe3d:camera_get_yaw()
NEG gunRot.y
INCR gunRot.y 90.0
fe3d:model_set_position("player", camPos.x, 0.0, camPos.z)
fe3d:model_set_position("m1911", gunPos.x, gunPos.y, gunPos.z)
fe3d:model_set_rotation("player", 0.0, gunRot.y, 0.0)
fe3d:model_set_rotation("m1911", gunRot.x, gunRot.y, gunRot.z)

BOOL walkStarted = fe3d:model_is_animation_started("player", "skeleton_walk")
IF isMoving IS <true> AND walkStarted IS <false>:
    fe3d:model_start_animation("player", "skeleton_walk", -1)
IF isMoving IS <false> AND walkStarted IS <true>:
    fe3d:model_fade_animation("player", "skeleton_walk", 1)

/// Move camera to eye position
fe3d:camera_follow_z(_skeletonEyeOffset)
