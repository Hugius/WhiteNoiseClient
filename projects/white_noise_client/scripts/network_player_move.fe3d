49 18
META script_type_update
META script_execution_waiting

/// Commonly used values
CONST STR walkID = "skeleton_walk"
STR skeletonID = ""
INT index = 0
INT maxIndex = misc:list_get_size("_playerMoveMessages")

/// Handle player movement messages
LIST messageParts = {}
STR part1 = ""
STR part2 = ""
STR part3 = ""
BOOL isJoined = <false>
LOOP:
    IF index IS maxIndex:
        BREAK
    ELSE:
        EDIT messageParts = misc:string_split(_playerMoveMessages[index], ":")
        EDIT part1 = messageParts[0]
        EDIT part2 = messageParts[1]
        EDIT skeletonID = misc:string_concat(part2, "_skeleton")
        EDIT isJoined = fe3d:model_is_existing(skeletonID)
        IF isJoined IS <true>:
            IF part1 IS "player_skeleton_position":
                EDIT part3 = messageParts[2]
                CAST part3 VEC3
                fe3d:model_set_position(skeletonID, part3.x, part3.y, part3.z)
                CAST part3 STR
            ELIF part1 IS "player_skeleton_rotation":
                EDIT part3 = messageParts[2]
                CAST part3 VEC3
                fe3d:model_set_rotation(skeletonID, part3.x, part3.y, part3.z)
                CAST part3 STR
            ELIF part1 IS "player_skeleton_walk_start":
                PUSH _playerWalksToStart part2
            ELIF part1 IS "player_skeleton_walk_stop":
                fe3d:model_fade_animation(skeletonID, walkID, 1)
            ELIF part1 IS "player_skeleton_walk_speed":
                EDIT part3 = messageParts[2]
                CAST part3 DEC
                fe3d:model_set_animation_speed(skeletonID, walkID, part3)
                CAST part3 STR
        INCR index 1

/// Start player walk animations
EDIT index = 0
EDIT maxIndex = misc:list_get_size("_playerWalksToStart")
BOOL isWalkStarted = <false>
LOOP:
    IF index IS maxIndex:
        BREAK
    ELSE:
        EDIT skeletonID = misc:string_concat(_playerWalksToStart[index], "_skeleton")
        EDIT isWalkStarted = fe3d:model_is_animation_started(skeletonID, walkID)
        IF isWalkStarted IS <false>:
            fe3d:model_start_animation(skeletonID, walkID, -1)
            PULL _playerWalksToStart index
            BREAK
        INCR index 1
