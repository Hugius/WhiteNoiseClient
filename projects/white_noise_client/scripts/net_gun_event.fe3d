68 16
META script_type_update
META script_execution_waiting

/// Constants
INT maxIndex = misc:list_get_size("_gunEventMessages")

/// Non-constants
LIST messageParts = {}
STR message = ""
STR rejectMessage = ""
STR part1 = ""
STR part2 = ""
STR part3 = ""
STR skelID = ""
STR gunID = ""
STR skelAnimID = ""
STR gunAnimID = ""
INT index = 0
BOOL isJoined = <false>
BOOL isStarted = <false>
BOOL isPaused = <false>

/// Handle player gun messages
LOOP:
    IF index IS maxIndex:
        BREAK
    EDIT message = _gunEventMessages[index]
    EDIT rejectMessage = misc:string_concat("reject:", message)
    EDIT messageParts = misc:string_split(message, ":")
    EDIT part1 = messageParts[0]
    EDIT part2 = messageParts[1]
    EDIT part3 = messageParts[2]
    EDIT skelID = misc:string_concat(part3, "_skeleton")
    EDIT gunID = misc:string_concat(part3, "_gun")
    EDIT isJoined = fe3d:model_is_existing(skelID)
    IF isJoined IS <true>:
        IF part1 IS "player" AND part2 IS "gun_equip":
            EDIT skelAnimID = "skeleton_gun_eqp"
            EDIT isStarted = fe3d:model_is_animation_started(skelID, skelAnimID)
            IF isStarted IS <false>:
                fe3d:model_start_animation(skelID, skelAnimID, 0)
                fe3d:model_set_animation_autopaused(skelID, skelAnimID, <true>)
            ELSE:
                fe3d:client_send_tcp_message(rejectMessage)
        ELIF part1 IS "player" AND part2 IS "gun_unequip":
            EDIT skelAnimID = "skeleton_gun_eqp"
            EDIT isStarted = fe3d:model_is_animation_started(skelID, skelAnimID)
            IF isStarted IS <true>:
                EDIT isPaused = fe3d:model_is_animation_paused(skelID, skelAnimID)
                IF isPaused IS <true>:
                    fe3d:model_resume_animation(skelID, skelAnimID)
                ELSE:
                    fe3d:client_send_tcp_message(rejectMessage)
            ELSE:
                fe3d:client_send_tcp_message(rejectMessage)
        ELIF part1 IS "player" AND part2 IS "gun_ads":
            EDIT skelAnimID = "skeleton_gun_ads"
            EDIT isStarted = fe3d:model_is_animation_started(skelID, skelAnimID)
            IF isStarted IS <true>:
                EDIT isPaused = fe3d:model_is_animation_paused(skelID, skelAnimID)
                IF isPaused IS <true>:
                    fe3d:model_resume_animation(skelID, skelAnimID)
                ELSE:
                    fe3d:client_send_tcp_message(rejectMessage)
            ELSE:
                fe3d:model_start_animation(skelID, skelAnimID, 0)
                fe3d:model_set_animation_autopaused(skelID, skelAnimID, <true>)
        ELIF part1 IS "player" AND part2 IS "gun_shoot":
            PASS
        ELIF part1 IS "player" AND part2 IS "gun_reload":
            EDIT skelAnimID = "skeleton_gun_rld"
            EDIT gunAnimID = "gun_reload"
            EDIT isStarted = fe3d:model_is_animation_started(skelID, skelAnimID)
            IF isStarted IS <false>:
                fe3d:model_start_animation(skelID, skelAnimID, 0)
                fe3d:model_start_animation(gunID, gunAnimID, 0)
    INCR index 1
