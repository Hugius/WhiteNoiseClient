22 0
META script_type_update
META script_execution_waiting

/// Update view bobbing
IF _isMoving IS <true> OR _cameraHeightOffset NOT 0.0:
    DEC bobbingSpeed = _bobbingSpeed
    IF _isSprinting IS <true>:
        MUL bobbingSpeed _sprintFactor
    MUL bobbingSpeed _bobbingDirection
    INCR _cameraHeightOffset bobbingSpeed
    DEC absOffset = math:abs(_cameraHeightOffset)
    IF absOffset MORE _maxBobbingOffset:
        MUL _bobbingDirection -1.0
    ELIF _isMoving IS <false> AND absOffset LESS 0.000001:
        EDIT _cameraHeightOffset = 0.0
        EDIT _bobbingDirection = 1.0

/// Update camera height
VEC3 camPos = fe3d:camera_get_position()
EDIT camPos.y = _defaultCameraHeight
INCR camPos.y _cameraHeightOffset
fe3d:camera_set_position(camPos.x, camPos.y, camPos.z)

/// Update free looking
BOOL fDown = fe3d:input_is_key_down("KEY_F")
IF _isReloading IS <false> AND _isGunEquipped IS <true>:
    EDIT _isFreeLooking = fDown
ELSE:
    EDIT _isFreeLooking = <false>

/// Update camera locking
IF _isFreeLooking IS <true>:
    fe3d:camera_unlock_pitch()
    fe3d:image_set_visible("crosshair", <false>)
ELSE:
    fe3d:camera_lock_pitch()
    DEC pitch = fe3d:camera_get_pitch()
    DEC absPitch = math:abs(pitch)
    DEC negSpeed = _pitchSpeed
    NEG negSpeed
    IF absPitch LESS 0.000001:
        EDIT pitch = 0.0
    ELIF pitch MORE _pitchSpeed:
        DECR pitch _pitchSpeed
    ELIF pitch LESS negSpeed:
        INCR pitch _pitchSpeed
    fe3d:camera_set_pitch(pitch)

/// Update first-person view
BOOL capsDown = fe3d:input_is_key_down("KEY_CAPSLOCK")
IF capsDown IS <true>:
    fe3d:camera_enable_first_person_view()
    fe3d:cursor_hide()
ELSE:
    fe3d:camera_disable_first_person_view()
    fe3d:cursor_show()
