24 33
META script_type_update
META script_execution_waiting

/// Constants
CONST STR legsID = "skeleton_walk_legs"
CONST STR armsID = "skeleton_walk_arms"
CONST INT maxIndex = misc:list_get_size("_skeletonEventMessages")

/// Non-constants
LIST messageParts = {}
STR message = ""
STR rejectMessage = ""
STR p1 = ""
STR p2 = ""
STR skelID = ""
INT index = 0
BOOL isJoined = <false>
BOOL isStarted = <false>
BOOL isFading = <false>

/// Handle player skeleton messages
LOOP:
    IF index IS maxIndex:
        BREAK
    EDIT message = _skeletonEventMessages[index]
    EDIT rejectMessage = misc:string_concat("reject:", message)
    EDIT messageParts = misc:string_split(message, ":")
    EDIT p1 = messageParts[0]
    EDIT p2 = messageParts[1]
    EDIT skelID = misc:string_concat(p2, "_skeleton")
    EDIT isJoined = fe3d:model_is_existing(skelID)
    IF isJoined IS <true>:
        IF p1 IS "player_skeleton_legs_start":
            EDIT isStarted = fe3d:model_is_animation_started(skelID, legsID)
            IF isStarted IS <false>:
                fe3d:model_start_animation(skelID, legsID, -1)
            ELSE:
                fe3d:client_send_tcp_message(rejectMessage)
        ELIF p1 IS "player_skeleton_legs_stop":
            EDIT isStarted = fe3d:model_is_animation_started(skelID, legsID)
            IF isStarted IS <true>:
                EDIT isFading = fe3d:model_is_animation_fading(skelID, legsID)
                IF isFading IS <false>:
                    fe3d:model_fade_animation(skelID, legsID, 1)
                ELSE:
                    fe3d:client_send_tcp_message(rejectMessage)
            ELSE:
                fe3d:client_send_tcp_message(rejectMessage)
        ELIF p1 IS "player_skeleton_arms_start":
            EDIT isStarted = fe3d:model_is_animation_started(skelID, armsID)
            IF isStarted IS <false>:
                fe3d:model_start_animation(skelID, armsID, -1)
            ELSE:
                fe3d:client_send_tcp_message(rejectMessage)
        ELIF p1 IS "player_skeleton_arms_stop":
            EDIT isStarted = fe3d:model_is_animation_started(skelID, armsID)
            IF isStarted IS <true>:
                EDIT isFading = fe3d:model_is_animation_fading(skelID, armsID)
                IF isFading IS <false>:
                    fe3d:model_fade_animation(skelID, armsID, 1)
                ELSE:
                    fe3d:client_send_tcp_message(rejectMessage)
            ELSE:
                fe3d:client_send_tcp_message(rejectMessage)
    INCR index 1
