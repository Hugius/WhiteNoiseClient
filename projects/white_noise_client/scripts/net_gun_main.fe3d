46 12
META script_type_update
META script_execution_waiting

/// Constants
INT maxIndex = misc:list_get_size("_gunMainMessages")

/// Non-constants
LIST messageParts = {}
STR message = ""
STR rejectMessage = ""
STR part1 = ""
STR part2 = ""
STR part3 = ""
STR part4 = ""
STR skelID = ""
STR gunID = ""
STR gunfireID = ""
STR gunfire1ID = ""
STR gunfire2ID = ""
VEC3 current = [0.0 0.0 0.0]
DEC difference = 0.0
INT index = 0
BOOL isJoined = <false>

/// Handle player gun messages
LOOP:
    IF index IS maxIndex:
        BREAK
    EDIT message = _gunMainMessages[index]
    EDIT rejectMessage = misc:string_concat("reject:", message)
    EDIT messageParts = misc:string_split(message, ":")
    EDIT part1 = messageParts[0]
    EDIT part2 = messageParts[1]
    EDIT skelID = misc:string_concat(part2, "_skeleton")
    EDIT gunID = misc:string_concat(part2, "_gun")
    EDIT gunfireID = misc:string_concat(part2, "_gunfire")
    EDIT gunfire1ID = misc:string_concat(part2, "_gunfire1")
    EDIT gunfire2ID = misc:string_concat(part2, "_gunfire2")
    EDIT isJoined = fe3d:model_is_existing(skelID)
    IF isJoined IS <true>:
        IF part1 IS "player_gun_position":
            EDIT part3 = messageParts[2]
            CAST part3 VEC3
            EDIT current = fe3d:model_get_position(gunID)
            EDIT difference = math:vector_distance(current, part3)
            DIV difference _positionInterpolationDivider
            fe3d:model_move_to(gunID, part3.x, part3.y, part3.z, difference)
            CAST part3 STR
        ELIF part1 IS "player_gun_rotation":
            EDIT part3 = messageParts[2]
            CAST part3 VEC3
            EDIT current = fe3d:model_get_rotation(gunID)
            EDIT difference = math:angle_difference(current.y, part3.y)
            DIV difference _rotationInterpolationDivider
            fe3d:model_rotate_to(gunID, part3.x, part3.y, part3.z, difference)
            CAST part3 STR
        ELIF part1 IS "player_gun_visibility":
            EDIT part3 = messageParts[2]
            CAST part3 BOOL
            fe3d:model_set_visible(gunID, part3)
            CAST part3 STR
        ELIF part1 IS "player_gunfire_position":
            EDIT part3 = messageParts[2]
            CAST part3 VEC3
            fe3d:billboard_set_position(gunfire1ID, part3.x, part3.y, part3.z)
            fe3d:billboard_set_position(gunfire2ID, part3.x, part3.y, part3.z)
            fe3d:light_set_position(gunfireID, part3.x, part3.y, part3.z)
            CAST part3 STR
        ELIF part1 IS "player_gunfire_rotation":
            EDIT part3 = messageParts[2]
            EDIT part4 = messageParts[3]
            CAST part3 VEC3
            CAST part4 VEC3
            fe3d:billboard_set_rotation(gunfire1ID, part3.x, part3.y, part3.z)
            fe3d:billboard_set_rotation(gunfire2ID, part4.x, part4.y, part4.z)
            CAST part3 STR
            CAST part4 STR
        ELIF part1 IS "player_gunfire_visibility":
            EDIT part3 = messageParts[2]
            CAST part3 BOOL
            fe3d:billboard_set_visible(gunfire1ID, part3)
            fe3d:billboard_set_visible(gunfire2ID, part3)
            fe3d:light_set_visible(gunfireID, part3)
            CAST part3 STR
    INCR index 1
