4 36
META script_type_update
META script_execution_waiting

/// Constants
INT maxIndex = misc:list_get_size("_gunMainMessages")

/// Non-constants
LIST messageParts = {}
STR message = ""
STR rejectMessage = ""
STR p1 = ""
STR p2 = ""
STR p3 = ""
STR p4 = ""
STR skelID = ""
STR gunID = ""
STR gunfireID = ""
STR gunfire1ID = ""
STR gunfire2ID = ""
INT index = 0
BOOL isJoined = <false>

/// Handle player gun messages
LOOP:
    IF index IS maxIndex:
        BREAK
    EDIT message = _gunMainMessages[index]
    EDIT rejectMessage = misc:string_concat("reject:", message)
    EDIT messageParts = misc:string_split(message, ":")
    EDIT p1 = messageParts[0]
    EDIT p2 = messageParts[1]
    EDIT skelID = misc:string_concat(p2, "_skeleton")
    EDIT gunID = misc:string_concat(p2, "_gun")
    EDIT gunfireID = misc:string_concat(p2, "_gunfire")
    EDIT gunfire1ID = misc:string_concat(p2, "_gunfire1")
    EDIT gunfire2ID = misc:string_concat(p2, "_gunfire2")
    EDIT isJoined = fe3d:model_is_existing(skelID)
    IF isJoined IS <true>:
        IF p1 IS "player_gun_position":
            EDIT p3 = messageParts[2]
            CAST p3 VEC3
            fe3d:model_set_position(gunID, p3.x, p3.y, p3.z)
            CAST p3 STR
        ELIF p1 IS "player_gun_rotation":
            EDIT p3 = messageParts[2]
            CAST p3 VEC3
            fe3d:model_set_rotation(gunID, p3.x, p3.y, p3.z)
            CAST p3 STR
        ELIF p1 IS "player_gun_visibility":
            EDIT p3 = messageParts[2]
            CAST p3 BOOL
            fe3d:model_set_visible(gunID, p3)
            CAST p3 STR
        ELIF p1 IS "player_gunfire_position":
            EDIT p3 = messageParts[2]
            CAST p3 VEC3
            fe3d:billboard_set_position(gunfire1ID, p3.x, p3.y, p3.z)
            fe3d:billboard_set_position(gunfire2ID, p3.x, p3.y, p3.z)
            fe3d:light_set_position(gunfireID, p3.x, p3.y, p3.z)
            CAST p3 STR
        ELIF p1 IS "player_gunfire_rotation":
            EDIT p3 = messageParts[2]
            EDIT p4 = messageParts[3]
            CAST p3 VEC3
            CAST p4 VEC3
            fe3d:billboard_set_rotation(gunfire1ID, p3.x, p3.y, p3.z)
            fe3d:billboard_set_rotation(gunfire2ID, p4.x, p4.y, p4.z)
            CAST p3 STR
            CAST p4 STR
        ELIF p1 IS "player_gunfire_visibility":
            EDIT p3 = messageParts[2]
            CAST p3 BOOL
            fe3d:billboard_set_visible(gunfire1ID, p3)
            fe3d:billboard_set_visible(gunfire2ID, p3)
            fe3d:light_set_visible(gunfireID, p3)
            CAST p3 STR
    INCR index 1
