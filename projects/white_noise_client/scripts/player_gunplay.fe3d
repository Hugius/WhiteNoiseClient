10 14
META script_type_update
META script_execution_waiting

/// Commonly used values
CONST STR skelID = "player_skeleton"
CONST STR gunID = misc:string_concat("player_", _equippedGunType)
STR skelAdsID = misc:string_concat("skeleton_", _equippedGunType)
EDIT skelAdsID = misc:string_concat(skelAdsID, "_ads")
STR gunShootID = misc:string_concat(_equippedGunType, "_shoot")
STR gunReloadID = misc:string_concat(_equippedGunType, "_reload")
STR skelReloadID = misc:string_concat("skeleton_", _equippedGunType)
EDIT skelReloadID = misc:string_concat(skelReloadID, "_rld")

/// Inputs
BOOL rPressed = fe3d:input_is_key_pressed("KEY_R")
BOOL lmbPressed = fe3d:input_is_mouse_pressed("BUTTON_LEFT")
BOOL lmbDown = fe3d:input_is_mouse_down("BUTTON_LEFT")
BOOL rmbPressed = fe3d:input_is_mouse_pressed("BUTTON_RIGHT")

/// Update ADS - toggling
BOOL isAdsStarted = fe3d:model_is_animation_started(skelID, skelAdsID)
BOOL isAdsPlaying = <false>
IF isAdsStarted  IS <true>:
    EDIT isAdsPlaying = fe3d:model_is_animation_playing(skelID, skelAdsID)
IF rmbPressed IS <true> AND isAdsPlaying IS <false>:
    IF _isGunReloading IS <false> AND _isGunEquipped IS <true>:
        IF _isGunAds IS <true>:
            fe3d:model_resume_animation(skelID, skelAdsID)
            EDIT _isGunAds = <false>
        ELSE:
            fe3d:model_start_animation(skelID, skelAdsID, 0)
            fe3d:model_set_animation_autopaused(skelID, skelAdsID, <true>)
            EDIT _isGunAds = <true>

/// Update ADS - aiming animation
IF isAdsStarted IS <true>:
    IF isAdsPlaying IS <true>:
        IF _isGunAds IS <true>:
            DECR _gunOffsetX _gunOffsetSpeedX
        ELSE:
            INCR _gunOffsetX _gunOffsetSpeedX
    ELSE:
        EDIT _gunOffsetX = _adsGunOffsetX
ELSE:
    EDIT _gunOffsetX = _defaultGunOffsetX

/// Update ADS - crosshair & FOV
DEC fov = fe3d:camera_get_fov()
IF _isGunAds IS <true>:
    IF fov MORE _adsFov:
        DECR fov _fovSpeed
    ELSE:
        EDIT fov = _adsFov
ELSE:
    IF fov LESS _defaultFov:
        INCR fov _fovSpeed
    ELSE:
        EDIT fov = _defaultFov
fe3d:camera_set_fov(fov)

/// Update shooting
EDIT _isGunShooting = <false>
BOOL isSemi = _gunDataSemi[_equippedGunIndex]
BOOL isShootStarted = fe3d:model_is_animation_started(gunID, gunShootID)
IF isShootStarted IS <false> AND _isGunReloading IS <false>:
    IF _isGunEquipped IS <true>:
        IF isSemi IS <true>:
            IF lmbPressed IS <true>:
                fe3d:model_start_animation(gunID, gunShootID, 0)
                fe3d:sound_play_forced(gunShootID, 0, 0)
                EDIT _isGunShooting = <true>
        ELSE:
            IF lmbDown IS <true>:
                fe3d:model_start_animation(gunID, gunShootID, 0)
                fe3d:sound_play_forced(gunShootID, 0, 0)
                EDIT _isGunShooting = <true>

/// Update reloading - starting animations
IF rPressed IS <true> AND _isGunAds IS <false> AND _isGunReloading IS <false>:
    IF _isGunEquipped IS <true>:
        EDIT _isGunReloading = <true>
        fe3d:model_start_animation(gunID, gunReloadID, 0)
        fe3d:model_start_animation(skelID, skelReloadID, 0)

/// Update reloading - cleanup
BOOL isReloadStarted = fe3d:model_is_animation_started(skelID, skelReloadID)
IF _isGunReloading IS <true> AND isReloadStarted IS <false>:
    EDIT _isGunReloading = <false>
