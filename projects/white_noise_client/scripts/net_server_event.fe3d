51 26
META script_type_update
META script_execution_waiting

/// Non-constants
LIST messageParts = {}
STR content = ""
INT index = 0
INT maxIndex = misc:list_size("_serverEventMessages")

/// Handle server event messages
LOOP:
    IF index IS maxIndex:
        BREAK
    ELSE:
        EDIT messageParts = misc:string_split(_serverEventMessages[index], ":")
        IF messageParts[0] IS "server" AND messageParts[1] IS "start":
            /// Load player
            EXEC player_init_main
            EDIT _isGameStarted = <true>
            /// Extract camera values
            STR position = messageParts[2]
            STR yaw = messageParts[3]
            CAST position VEC3
            CAST yaw DEC
            EDIT _startingPosition = position
            EDIT _startingYaw = yaw
            /// Set camera values
            fe3d:camera_set_position(position.x, _defaultCameraHeight, position.z)
            fe3d:camera_set_yaw(yaw)
        ELIF messageParts[0] IS "server" AND messageParts[1] IS "next":
            STR round = messageParts[2]
            EDIT content = misc:string_concat("Round: ", round)
            fe3d:text_set_content("round", content)
            VEC3 position = _startingPosition
            DEC yaw = _startingYaw
            fe3d:camera_set_position(position.x, _defaultCameraHeight, position.z)
            fe3d:camera_set_yaw(yaw)
            fe3d:camera_set_pitch(0.0)
        ELIF messageParts[0] IS "server" AND messageParts[1] IS "stop":
            /// GUI
            fe3d:image_set_visible("black", <true>)
            STR winner = messageParts[2]
            STR kills = messageParts[3]
            EDIT content = misc:string_concat(winner, " won the game with ")
            EDIT content = misc:string_concat(content, kills)
            EDIT content = misc:string_concat(content, " kills!")
            fe3d:text_set_content("winner", content)
            INT size = misc:string_get_size(content)
            CAST size DEC
            MUL size 0.03
            fe3d:text_set_size("winner", size, 0.1)
            /// Disconnect
            fe3d:client_disconnect()
            /// Miscellaneous
            fe3d:camera_disable_first_person_view()
            fe3d:cursor_set_visible(<true>)
            EDIT _isGameStarted = <false>
        INCR index 1
