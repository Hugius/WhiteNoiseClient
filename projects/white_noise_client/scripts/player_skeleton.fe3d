29 12
META script_type_update
META script_execution_waiting

/// Constants
CONST STR username = fe3d:client_get_username()
CONST STR skelID = "player_skeleton"
CONST STR legsID = "skeleton_walk_legs"
CONST STR armsID = "skeleton_walk_arms"
CONST BOOL isLegsStarted = fe3d:model_is_animation_started(skelID, legsID)
CONST BOOL isArmsStarted = fe3d:model_is_animation_started(skelID, armsID)

/// Non-constants
STR message = ""

/// Start legs animation
IF _isMoving IS <true> AND isLegsStarted IS <false>:
    EDIT message = misc:string_concat("forward:player_skeleton_legs_start:", username)
    fe3d:model_start_animation(skelID, legsID, -1)
    /// Send message to server
    fe3d:client_send_tcp_message(message)

/// Start arms animation
IF _isMoving IS <true> AND isArmsStarted IS <false> AND _isGunEquipped IS <false>:
    EDIT message = misc:string_concat("forward:player_skeleton_arms_start:", username)
    IF _isGunEquipping IS <false> AND _isGunUnequipping IS <false>:
        INT frame = fe3d:model_get_animation_frame_index(skelID, legsID)
        IF frame IS 0:
            fe3d:model_start_animation(skelID, armsID, -1)
            /// Send message to server
            fe3d:client_send_tcp_message(message)

/// Stop legs animation
IF isLegsStarted IS <true> AND _isMoving IS <false>:
    EDIT message = misc:string_concat("forward:player_skeleton_legs_stop:", username)
    BOOL isLegsFading = fe3d:model_is_animation_fading(skelID, legsID)
    IF isLegsFading IS <false>:
        fe3d:model_fade_animation(skelID, legsID, 1)
        /// Send message to server
        fe3d:client_send_tcp_message(message)

/// Stop arms animation
IF isArmsStarted IS <true>:
    EDIT message = misc:string_concat("forward:player_skeleton_arms_stop:", username)
    IF _isMoving IS <false> OR _wasMouseScrolled IS <true>:
        BOOL isArmsFading = fe3d:model_is_animation_fading(skelID, armsID)
        IF isArmsFading IS <false>:
            fe3d:model_fade_animation(skelID, armsID, 1)
            /// Send message to server
            fe3d:client_send_tcp_message(message)

/// Update legs animation speed
IF isLegsStarted IS <true>:
    IF _isSprinting IS <true>:
        DEC legsSpeed = _sprintFactor
    ELSE:
        DEC legsSpeed = 1.0
    fe3d:model_set_animation_speed(skelID, legsID, legsSpeed)

/// Update arms animation speed
IF isArmsStarted IS <true>:
    IF _isSprinting IS <true>:
        DEC armsSpeed = _sprintFactor
    ELSE:
        DEC armsSpeed = 1.0
    fe3d:model_set_animation_speed(skelID, armsID, armsSpeed)
