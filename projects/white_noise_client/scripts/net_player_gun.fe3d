54 78
META script_type_update
META script_execution_waiting

/// Values
LIST messageParts = {}
STR part1 = ""
STR part2 = ""
STR part3 = ""
STR part4 = ""
STR part5 = ""
STR skeletonID = ""
STR gunID = ""
STR armID = ""
INT armIndex = 0
INT index = 0
INT maxIndex = misc:list_get_size("_playerGunMessages")
BOOL isJoined = <false>
BOOL isArmStarted = <false>
BOOL isArmPaused = <false>

/// Handle player gun messages
LOOP:
    IF index IS maxIndex:
        BREAK
    EDIT messageParts = misc:string_split(_playerGunMessages[index], ":")
    EDIT part1 = messageParts[0]
    EDIT part2 = messageParts[1]
    EDIT part3 = messageParts[2]
    EDIT skeletonID = misc:string_concat(part2, "_skeleton")
    EDIT gunID = misc:string_concat(part2, "_")
    EDIT gunID = misc:string_concat(gunID, part3)
    EDIT isJoined = fe3d:model_is_existing(skeletonID)
    IF isJoined IS <true>:
        IF part1 IS "player_gun_position" AND gunID NOT "":
            EDIT part4 = messageParts[3]
            CAST part4 VEC3
            fe3d:model_set_position(gunID, part4.x, part4.y, part4.z)
            CAST part4 STR
        ELIF part1 IS "player_gun_rotation" AND gunID NOT "":
            EDIT part4 = messageParts[3]
            CAST part4 VEC3
            fe3d:model_set_rotation(gunID, part4.x, part4.y, part4.z)
            CAST part4 STR
        ELIF part1 IS "player_gun_equip_status":
            EDIT part4 = messageParts[3]
            EDIT part5 = messageParts[4]
            CAST part4 BOOL
            CAST part5 BOOL
            EDIT armID = misc:string_concat("skeleton_", part3)
            EDIT armID = misc:string_concat(armID, "_eqp")
            EDIT isArmStarted = fe3d:model_is_animation_started(skeletonID, armID)
            /// Equipping gun
            IF part4 IS <true> AND isArmStarted IS <false>:
                fe3d:model_start_animation(skeletonID, armID, 0)
                fe3d:model_set_animation_autopaused(skeletonID, armID, <true>)
            /// Unequipping gun
            IF part5 IS <true> AND isArmStarted IS <true>:
                EDIT isArmPaused = fe3d:model_is_animation_paused(skeletonID, armID)
                IF isArmPaused IS <true>:
                    fe3d:model_resume_animation(skeletonID, armID)
            /// Gun visibility
            fe3d:model_set_visible(gunID, isArmStarted)
            CAST part4 STR
            CAST part5 STR
    INCR index 1
