33 41
META script_type_update
META script_execution_waiting

/// Non-constants
LIST messageParts = {}
STR part1 = ""
STR part2 = ""
STR part3 = ""
STR part4 = ""
STR part5 = ""
STR skelID = ""
STR gunID = ""
STR skelAnimID = ""
STR gunAnimID = ""
INT index = 0
INT maxIndex = misc:list_get_size("_playerGunMessages")
BOOL isJoined = <false>
BOOL isStarted = <false>
BOOL isPaused = <false>

/// Handle player gun messages
LOOP:
    IF index IS maxIndex:
        BREAK
    EDIT messageParts = misc:string_split(_playerGunMessages[index], ":")
    EDIT part1 = messageParts[0]
    EDIT part2 = messageParts[1]
    EDIT part3 = messageParts[2]
    EDIT skelID = misc:string_concat(part2, "_skeleton")
    EDIT gunID = misc:string_concat(part2, "_")
    EDIT gunID = misc:string_concat(gunID, part3)
    EDIT isJoined = fe3d:model_is_existing(skelID)
    IF isJoined IS <true>:
        IF part1 IS "player_gun_position":
            EDIT part4 = messageParts[3]
            CAST part4 VEC3
            fe3d:model_set_position(gunID, part4.x, part4.y, part4.z)
            CAST part4 STR
        ELIF part1 IS "player_gun_rotation":
            EDIT part4 = messageParts[3]
            CAST part4 VEC3
            fe3d:model_set_rotation(gunID, part4.x, part4.y, part4.z)
            CAST part4 STR
        ELIF part1 IS "player_gun_visibility":
            EDIT part4 = messageParts[3]
            CAST part4 BOOL
            fe3d:model_set_visible(gunID, part4)
            CAST part4 STR
        ELIF part1 IS "player_gun_equip":
            EDIT skelAnimID = misc:string_concat("skeleton_", part3)
            EDIT skelAnimID = misc:string_concat(skelAnimID, "_eqp")
            fe3d:model_start_animation(skelID, skelAnimID, 0)
            fe3d:model_set_animation_autopaused(skelID, skelAnimID, <true>)
        ELIF part1 IS "player_gun_unequip":
            EDIT skelAnimID = misc:string_concat("skeleton_", part3)
            EDIT skelAnimID = misc:string_concat(skelAnimID, "_eqp")
            fe3d:model_resume_animation(skelID, skelAnimID)
        ELIF part1 IS "player_gun_ads":
            EDIT skelAnimID = misc:string_concat("skeleton_", part3)
            EDIT skelAnimID = misc:string_concat(skelAnimID, "_ads")
            EDIT isStarted = fe3d:model_is_animation_started(skelID, skelAnimID)
            IF isStarted IS <true>:
                EDIT isPaused = fe3d:model_is_animation_paused(skelID, skelAnimID)
                IF isPaused IS <true>:
                    fe3d:model_resume_animation(skelID, skelAnimID)
            ELSE:
                fe3d:model_start_animation(skelID, skelAnimID, 0)
                fe3d:model_set_animation_autopaused(skelID, skelAnimID, <true>)
        ELIF part1 IS "player_gun_shoot":
            PASS
        ELIF part1 IS "player_gun_reload":
            EDIT skelAnimID = misc:string_concat("skeleton_", part3)
            EDIT skelAnimID = misc:string_concat(skelAnimID, "_rld")
            EDIT gunAnimID = misc:string_concat(part3, "_reload")
            EDIT isStarted = fe3d:model_is_animation_started(skelID, skelAnimID)
            fe3d:model_start_animation(skelID, skelAnimID, 0)
            fe3d:model_start_animation(gunID, gunAnimID, 0)
    INCR index 1
