58 42
META script_type_update
META script_execution_waiting

/// Constants
CONST DEC charWidth = 0.1
CONST DEC charHeight = 0.2

/// Non-constants
LIST stringParts = {}
STR username = ""
STR tagID = ""
STR skelID = ""
STR m1911ID = ""
STR ak47ID = ""
STR gunfire1ID = ""
STR gunfire2ID = ""
INT index = 0
INT maxIndex = misc:list_get_size("_playerMainMessages")
INT usernameLength = 0
DEC tagWidth = 0.0

/// Handle player main messages
LOOP:
    IF index IS maxIndex:
        BREAK
    ELSE:
        EDIT stringParts = misc:string_split(_playerMainMessages[index], ":")
        EDIT username = stringParts[1]
        EDIT usernameLength = misc:string_get_size(username)
        EDIT tagID = misc:string_concat(username, "_tag")
        EDIT skelID = misc:string_concat(username, "_skeleton")
        EDIT m1911ID = misc:string_concat(username, "_m1911")
        EDIT ak47ID = misc:string_concat(username, "_ak47")
        EDIT gunfire1ID = misc:string_concat(username, "_gunfire1")
        EDIT gunfire2ID = misc:string_concat(username, "_gunfire2")
        IF stringParts[0] IS "player_join":
            /// Skeleton + guns
            fe3d:model_place(skelID, "skeleton", 0.0, 0.0, 0.0)
            fe3d:model_place(m1911ID, "m1911", 0.0, 0.0, 0.0)
            fe3d:model_set_visible(m1911ID, <false>)
            fe3d:model_place(ak47ID, "ak47", 0.0, 0.0, 0.0)
            fe3d:model_set_visible(ak47ID, <false>)
            /// Playertag
            EDIT tagWidth = charWidth
            CAST usernameLength DEC
            MUL tagWidth usernameLength
            CAST usernameLength INT
            fe3d:billboard_place(tagID, "tag", 0.0, 0.0, 0.0)
            fe3d:billboard_set_text(tagID, username)
            fe3d:billboard_set_size(tagID, tagWidth, charHeight)
            /// Gunfire
            fe3d:billboard_place(gunfire1ID, "gunfire", 0.0, 0.0, 0.0)
            fe3d:billboard_place(gunfire2ID, "gunfire", 0.0, 0.0, 0.0)
        ELIF stringParts[0] IS "player_leave":
            fe3d:model_delete(skelID)
            fe3d:model_delete(m1911ID)
            fe3d:model_delete(ak47ID)
            fe3d:billboard_delete(tagID)
            fe3d:billboard_delete(gunfire1ID)
            fe3d:billboard_delete(gunfire2ID)
        INCR index 1
