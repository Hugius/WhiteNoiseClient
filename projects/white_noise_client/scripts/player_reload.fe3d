36 57
META script_type_update
META script_execution_waiting

/// Constants
CONST STR skelID = "player_skeleton"
CONST STR gunID = misc:string_concat("player_", _equippedGunType)
CONST STR username = fe3d:client_get_username()

/// Commonly used values
STR skelAdsID = misc:string_concat("skeleton_", _equippedGunType)
EDIT skelAdsID = misc:string_concat(skelAdsID, "_ads")
STR skelReloadID = misc:string_concat("skeleton_", _equippedGunType)
EDIT skelReloadID = misc:string_concat(skelReloadID, "_rld")
STR gunReloadID = misc:string_concat(_equippedGunType, "_reload")
STR message = ""

/// Inputs
BOOL rPressed = fe3d:input_is_key_pressed("KEY_R")

/// Update reloading - start
IF rPressed IS <true> AND _isGunReloading IS <false> AND _isGunEquipped IS <true>:
    EDIT _isGunReloading = <true>
    /// Start reload animations
    fe3d:model_start_animation(skelID, skelReloadID, 0)
    fe3d:model_start_animation(gunID, gunReloadID, 0)
    /// Send reload message to server
    EDIT message = misc:string_concat("player_gun_reload:", username)
    EDIT message = misc:string_concat(message, ":")
    EDIT message = misc:string_concat(message, _equippedGunType)
    fe3d:client_send_tcp_message(message)
    /// Check if aiming down sight
    IF _isGunAds IS <true>:
        /// Stop ADS
        fe3d:model_resume_animation(skelID, skelAdsID)
        EDIT _isGunAds = <false>
        /// Send ADS message to server
        EDIT message = misc:string_concat("player_gun_ads:", username)
        EDIT message = misc:string_concat(message, ":")
        EDIT message = misc:string_concat(message, _equippedGunType)
        fe3d:client_send_tcp_message(message)

/// Update reloading - stop
BOOL isSkelReloadStarted = fe3d:model_is_animation_started(skelID, skelReloadID)
BOOL isGunReloadStarted = fe3d:model_is_animation_started(gunID, gunReloadID)
IF _isGunReloading IS <true>:
    IF isSkelReloadStarted IS <false> AND isGunReloadStarted IS <false>:
        EDIT _isGunReloading = <false>
