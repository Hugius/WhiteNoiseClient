49 54
META script_type_update
META script_execution_waiting

/// Constants
INT maxIndex = misc:list_get_size("_lightMainMessages")

/// Non-constants
LIST messageParts = {}
STR message = ""
STR part1 = ""
STR part2 = ""
STR part3 = ""
STR part4 = ""
STR skelID = ""
STR lightID = ""
VEC3 current = [0.0 0.0 0.0]
DEC difference = 0.0
INT index = 0
BOOL isJoined = <false>

/// Handle light messages
LOOP:
    IF index IS maxIndex:
        BREAK
    EDIT message = _lightMainMessages[index]
    EDIT messageParts = misc:string_split(message, ":")
    EDIT part1 = messageParts[0]
    EDIT part2 = messageParts[1]
    EDIT part3 = messageParts[2]
    EDIT skelID = misc:string_concat(part3, "_skeleton")
    EDIT lightID = misc:string_concat(part3, "_light")
    EDIT isJoined = fe3d:model_is_existing(skelID)
    IF isJoined IS <true>:
        IF part1 IS "player" AND part2 IS "light_position":
            EDIT part4 = messageParts[3]
            CAST part4 VEC3
            EDIT current = fe3d:spotlight_get_position(lightID)
            EDIT difference = math:vector_distance(current, part4)
            DIV difference _positionInterpolationDivider
            fe3d:spotlight_move_to(lightID, part4.x, part4.y, part4.z, difference)
            CAST part4 STR
        ELIF part1 IS "player" AND part2 IS "light_front":
            EDIT part4 = messageParts[3]
            CAST part4 VEC3
            fe3d:spotlight_set_front_vector(lightID, part4.x, part4.y, part4.z)
            CAST part4 STR
        ELIF part1 IS "player" AND part2 IS "light_visibility":
            EDIT part4 = messageParts[3]
            CAST part4 BOOL
            fe3d:spotlight_set_visible(lightID, part4)
            CAST part4 STR
    INCR index 1
