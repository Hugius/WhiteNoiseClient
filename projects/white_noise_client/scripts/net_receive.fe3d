32 24
META script_type_update
META script_execution_waiting

/// Constants
CONST LIST protocols = fe3d:client_get_pending_protocols()
CONST LIST messages = fe3d:client_get_pending_contents()

/// Commonly used values
LIST udpMessages = {}
INT index = 0
INT maxIndex = 0
BOOL contains = <false>

/// Clear messages
EDIT _receivedMessages = {}

/// Sort messages based on protocol
STR log = ""
EDIT index = 0
EDIT maxIndex = misc:list_get_size("messages")
LOOP:
    IF index IS maxIndex:
        BREAK
    EDIT contains = misc:string_contains(messages[index], ":")
    IF contains IS <true>:
        IF protocols[index] IS "TCP":
            PUSH _receivedMessages messages[index]
        ELSE:
            PUSH udpMessages messages[index]
    ELSE:
        EDIT log = misc:string_concat("Received invalid message: ", messages[index])
        fe3d:print(log)
        fe3d:application_stop()
    INCR index 1

/// Filter UDP messages
LIST messageParts = {}
LIST udpHeaders = {}
EDIT udpMessages = misc:list_reverse("udpMessages")
STR udpHeader = ""
EDIT index = 0
EDIT maxIndex = misc:list_get_size("udpMessages")
LOOP:
    IF index IS maxIndex:
        BREAK
    EDIT messageParts = misc:string_split(udpMessages[index], ":")
    EDIT udpHeader = misc:string_concat(messageParts[0], messageParts[1])
    EDIT contains = misc:list_contains("udpHeaders", udpHeader)
    IF contains IS <false>:
        PUSH udpHeaders udpHeader
        PUSH _receivedMessages udpMessages[index]
    INCR index 1
