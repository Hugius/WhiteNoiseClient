25 65
META script_type_update
META script_execution_waiting

/// Constants
CONST STR skelID = "player_skeleton"
CONST STR gunID = "player_gun"
CONST STR skelAdsID = "skeleton_gun_ads"
CONST STR username = fe3d:client_get_username()
CONST BOOL rmbPressed = fe3d:input_is_mouse_pressed("BUTTON_RIGHT")

/// Non-constants
BOOL isAdsStarted = fe3d:model_is_animation_started(skelID, skelAdsID)
BOOL isAdsPlaying = <false>
IF isAdsStarted IS <true>:
    EDIT isAdsPlaying = fe3d:model_is_animation_playing(skelID, skelAdsID)

/// Update ADS - toggling
IF rmbPressed IS <true> OR _wasMouseScrolled IS <true>:
    IF isAdsPlaying IS <false>:
        IF _isGunReloading IS <false> AND _isGunEquipped IS <true>:
            IF _isGunAds IS <true>:
                fe3d:model_resume_animation(skelID, skelAdsID)
                EDIT _isGunAds = <false>
            ELSE:
                fe3d:model_start_animation(skelID, skelAdsID, 0)
                fe3d:model_set_animation_autopaused(skelID, skelAdsID, <true>)
                EDIT _isGunAds = <true>
            /// Send gun ADS status to server
            STR message = misc:string_concat("forward:player_gun_ads:", username)
            fe3d:client_send_tcp_message(message)

/// Update ADS - aiming animation
IF isAdsStarted IS <true>:
    IF isAdsPlaying IS <true>:
        IF _isGunAds IS <true>:
            DECR _gunOffsetX _gunOffsetSpeedX
        ELSE:
            INCR _gunOffsetX _gunOffsetSpeedX
    ELSE:
        EDIT _gunOffsetX = _adsGunOffsetX
ELSE:
    EDIT _gunOffsetX = _defaultGunOffsetX

/// Update ADS - camera FOV
DEC fov = fe3d:camera_get_fov()
IF _isGunAds IS <true>:
    IF fov MORE _adsFov:
        DECR fov _fovSpeed
    ELSE:
        EDIT fov = _adsFov
ELSE:
    IF fov LESS _defaultFov:
        INCR fov _fovSpeed
    ELSE:
        EDIT fov = _defaultFov
fe3d:camera_set_fov(fov)
