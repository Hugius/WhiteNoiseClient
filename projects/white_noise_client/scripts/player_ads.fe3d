6 47
META script_type_update
META script_execution_waiting

/// Constants
CONST STR skelID = "player_skeleton"
CONST STR gunID = misc:string_concat("player_", _equippedGunType)
CONST STR username = fe3d:client_get_username()
CONST BOOL rmbPressed = fe3d:input_is_mouse_pressed("BUTTON_RIGHT")

/// Non-constants
STR skelAdsID = misc:string_concat("skeleton_", _equippedGunType)
EDIT skelAdsID = misc:string_concat(skelAdsID, "_ads")
BOOL isSkelAdsStarted = fe3d:model_is_animation_started(skelID, skelAdsID)
BOOL isSkelAdsPlaying = <false>
IF isSkelAdsStarted IS <true>:
    EDIT isSkelAdsPlaying = fe3d:model_is_animation_playing(skelID, skelAdsID)

/// Update ADS - toggling
IF rmbPressed IS <true> OR _wasMouseScrolled IS <true>:
    IF isSkelAdsPlaying IS <false>:
        IF _isGunReloading IS <false> AND _isGunEquipped IS <true>:
            IF _isGunAds IS <true>:
                fe3d:model_resume_animation(skelID, skelAdsID)
                EDIT _isGunAds = <false>
            ELSE:
                fe3d:model_start_animation(skelID, skelAdsID, 0)
                fe3d:model_set_animation_autopaused(skelID, skelAdsID, <true>)
                EDIT _isGunAds = <true>
            /// Send gun ADS status to server
            STR message = misc:string_concat("player_gun_ads:", username)
            EDIT message = misc:string_concat(message, ":")
            EDIT message = misc:string_concat(message, _equippedGunType)
            fe3d:client_send_tcp_message(message)

/// Update ADS - aiming animation
IF isSkelAdsStarted IS <true>:
    IF isSkelAdsPlaying IS <true>:
        IF _isGunAds IS <true>:
            DECR _gunOffsetX _gunOffsetSpeedX
        ELSE:
            INCR _gunOffsetX _gunOffsetSpeedX
    ELSE:
        EDIT _gunOffsetX = _adsGunOffsetX
ELSE:
    EDIT _gunOffsetX = _defaultGunOffsetX

/// Update ADS - camera FOV
DEC fov = fe3d:camera_get_fov()
IF _isGunAds IS <true>:
    IF fov MORE _adsFov:
        DECR fov _fovSpeed
    ELSE:
        EDIT fov = _adsFov
ELSE:
    IF fov LESS _defaultFov:
        INCR fov _fovSpeed
    ELSE:
        EDIT fov = _defaultFov
fe3d:camera_set_fov(fov)
