3 13
META script_type_update
META script_execution_waiting

/// Constants
CONST BOOL wDown = fe3d:input_is_key_down("KEY_W")
CONST BOOL aDown = fe3d:input_is_key_down("KEY_A")
CONST BOOL sDown = fe3d:input_is_key_down("KEY_S")
CONST BOOL dDown = fe3d:input_is_key_down("KEY_D")
CONST BOOL shiftDown = fe3d:input_is_key_down("KEY_LSHIFT")
CONST BOOL ctrlDown = fe3d:input_is_key_down("KEY_LCTRL")

/// Move eye offset
DEC offset = _skeletonEyeOffset
NEG offset
fe3d:camera_follow_z(offset)

/// Update movement speed
EDIT _isMoving = <false>
EDIT _isSprinting = <false>
MUL _movementSpeedX _movementSlowingFactor
MUL _movementSpeedZ _movementSlowingFactor
DEC minSpeed = _maxMovementSpeed
NEG minSpeed
DEC maxSpeed = _maxMovementSpeed
DEC acceleration = _movementAcceleration
BOOL isSprinting = <false>
IF shiftDown IS <true> AND wDown IS <true> AND _isGunEquipped IS <false>:
    IF _isGunEquipping IS <false> AND _isGunUnequipping IS <false>:
        MUL acceleration _sprintFactor
        MUL minSpeed _sprintFactor
        MUL maxSpeed _sprintFactor
        EDIT isSprinting = <true>

/// Update movement inputs
IF wDown IS <true>:
    INCR _movementSpeedZ acceleration
    EDIT _isMoving = <true>
    EDIT _isSprinting = isSprinting
IF sDown IS <true>:
    DECR _movementSpeedZ acceleration
    EDIT _isMoving = <true>
    EDIT _isSprinting = isSprinting
IF dDown IS <true>:
    INCR _movementSpeedX acceleration
    EDIT _isMoving = <true>
    EDIT _isSprinting = isSprinting
IF aDown IS <true>:
    DECR _movementSpeedX acceleration
    EDIT _isMoving = <true>
    EDIT _isSprinting = isSprinting

/// Update movement
EDIT _movementSpeedX = math:clamp(_movementSpeedX, minSpeed, maxSpeed)
EDIT _movementSpeedZ = math:clamp(_movementSpeedZ, minSpeed, maxSpeed)
fe3d:camera_follow_x(_movementSpeedX)
fe3d:camera_follow_z(_movementSpeedZ)
