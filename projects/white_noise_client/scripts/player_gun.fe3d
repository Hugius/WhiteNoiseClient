85 77
META script_type_update
META script_execution_waiting

/// Commonly used values
STR primaryGunModelID = misc:string_concat("player_", _primaryGunType)
STR adsID = misc:string_concat("skeleton_", _primaryGunType)
EDIT adsID = misc:string_concat(adsID, "_ads")
STR gunShootID = misc:string_concat(_primaryGunType, "_shoot")
STR gunReloadID = misc:string_concat(_primaryGunType, "_reload")
STR armReloadID = misc:string_concat("skeleton_", _primaryGunType)
EDIT armReloadID = misc:string_concat(armReloadID, "_rld")

/// Inputs
BOOL rPressed = fe3d:input_is_key_pressed("KEY_R")
BOOL lmbPressed = fe3d:input_is_mouse_pressed("BUTTON_LEFT")
BOOL lmbDown = fe3d:input_is_mouse_down("BUTTON_LEFT")
BOOL rmbPressed = fe3d:input_is_mouse_pressed("BUTTON_RIGHT")

/// Update ADS - toggling
BOOL isAdsPlaying = <false>
BOOL isAdsStarted = fe3d:model_is_animation_started("player_skeleton", adsID)
IF isAdsStarted  IS <true>:
    EDIT isAdsPlaying = fe3d:model_is_animation_playing("player_skeleton", adsID)
IF rmbPressed IS <true> AND isAdsPlaying IS <false>:
    IF _isReloading IS <false> AND _isFreeLooking IS <false>:
        IF _isGunEquipped IS <true>:
            IF _isADS IS <true>:
                fe3d:model_resume_animation("player_skeleton", adsID)
                EDIT _isADS = <false>
                EDIT _targetYawOffset = _defaultYawOffset
            ELSE:
                fe3d:model_start_animation("player_skeleton", adsID, 0, <false>)
                EDIT _isADS = <true>
                EDIT _targetYawOffset = 0.0

/// Update ADS - aiming animation
IF _yawOffset LESS _targetYawOffset:
    INCR _yawOffset _yawOffsetSpeed
ELIF _yawOffset MORE _targetYawOffset:
    DECR _yawOffset _yawOffsetSpeed

/// Update ADS - crosshair & FOV
DEC fov = fe3d:camera_get_fov()
IF _isADS IS <true>:
    IF fov MORE _adsFov:
        DECR fov _fovSpeed
    ELSE:
        EDIT fov = _adsFov
    fe3d:image_set_visible("crosshair", <false>)
ELSE:
    IF fov LESS _defaultFov:
        INCR fov _fovSpeed
    ELSE:
        EDIT fov = _defaultFov
    fe3d:image_set_visible("crosshair", <true>)
fe3d:camera_set_fov(fov)

/// Update ADS - arm animation
IF _isADS IS <true>:
    BOOL isArmStarted = fe3d:model_is_animation_started("player_skeleton", adsID)
    IF isArmStarted IS <true>:
        BOOL isPlaying = fe3d:model_is_animation_playing("player_skeleton", adsID)
        INT index = fe3d:model_get_animation_frame_index("player_skeleton", adsID)
        IF isPlaying IS <true> AND index IS 2:
            fe3d:model_pause_animation("player_skeleton", adsID)

/// Update shooting
BOOL isSemi = _gunDataSemi[_primaryGunTypeIndex]
BOOL isShootStarted = fe3d:model_is_animation_started(primaryGunModelID, gunShootID)
IF isShootStarted IS <false> AND _isReloading IS <false>:
    IF _isGunEquipped IS <true> AND _isFreeLooking IS <false>:
        IF isSemi IS <true>:
            IF lmbPressed IS <true>:
                fe3d:model_start_animation(primaryGunModelID, gunShootID, 0, <false>)
                fe3d:sound_play_forced(gunShootID, 0, 0)
        ELSE:
            IF lmbDown IS <true>:
                fe3d:model_start_animation(primaryGunModelID, gunShootID, 0, <false>)
                fe3d:sound_play_forced(gunShootID, 0, 0)

/// Update reloading - starting animations
IF rPressed IS <true> AND _isADS IS <false> AND _isReloading IS <false>:
    IF _isFreeLooking IS <false>:
        EDIT _isReloading = <true>
        fe3d:model_start_animation(primaryGunModelID, gunReloadID, 0, <false>)
        fe3d:model_start_animation("player_skeleton", armReloadID, 0, <false>)

/// Update reloading - cleanup
BOOL isReloadStarted = fe3d:model_is_animation_started("player_skeleton", armReloadID)
IF _isReloading IS <true> AND isReloadStarted IS <false>:
    EDIT _isReloading = <false>
