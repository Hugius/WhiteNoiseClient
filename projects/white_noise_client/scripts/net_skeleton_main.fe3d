40 24
META script_type_update
META script_execution_waiting

/// Constants
CONST STR legsID = "skeleton_walk_legs"
CONST STR armsID = "skeleton_walk_arms"
CONST INT maxIndex = misc:list_get_size("_skeletonMainMessages")

/// Non-constants
LIST messageParts = {}
STR message = ""
STR rejectMessage = ""
STR p1 = ""
STR p2 = ""
STR p3 = ""
STR p4 = ""
STR p5 = ""
STR skelID = ""
STR tagID = ""
DEC speed = 0.0
INT index = 0
BOOL isJoined = <false>
BOOL isStarted = <false>

/// Handle player skeleton messages
LOOP:
    IF index IS maxIndex:
        BREAK
    EDIT message = _skeletonMainMessages[index]
    EDIT rejectMessage = misc:string_concat("reject:", message)
    EDIT messageParts = misc:string_split(message, ":")
    EDIT p1 = messageParts[0]
    EDIT p2 = messageParts[1]
    EDIT skelID = misc:string_concat(p2, "_skeleton")
    EDIT isJoined = fe3d:model_is_existing(skelID)
    IF isJoined IS <true>:
        IF p1 IS "player_skeleton_position":
            EDIT p3 = messageParts[2]
            EDIT p4 = messageParts[3]
            CAST p3 VEC3
            CAST p4 VEC3
            fe3d:model_move_to(skelID, p3.x, p3.y, p3.z, p4.x, p4.y, p4.z)
            EDIT tagID = misc:string_concat(p2, "_tag")
            INCR p3.y _tagHeight
            fe3d:billboard_set_position(tagID, p3.x, p3.y, p3.z)
            CAST p3 STR
            CAST p4 STR
        ELIF p1 IS "player_skeleton_rotation":
            EDIT p3 = messageParts[2]
            CAST p3 VEC3
            fe3d:model_set_rotation(skelID, p3.x, p3.y, p3.z)
            CAST p3 STR
        ELIF p1 IS "player_skeleton_legs_speed":
            EDIT isStarted = fe3d:model_is_animation_started(skelID, legsID)
            IF isStarted IS <true>:
                EDIT p3 = messageParts[2]
                CAST p3 DEC
                fe3d:model_set_animation_speed(skelID, legsID, p3)
                CAST p3 STR
        ELIF p1 IS "player_skeleton_arms_speed":
            EDIT isStarted = fe3d:model_is_animation_started(skelID, armsID)
            IF isStarted IS <true>:
                EDIT p3 = messageParts[2]
                CAST p3 DEC
                fe3d:model_set_animation_speed(skelID, armsID, p3)
                CAST p3 STR
    INCR index 1
