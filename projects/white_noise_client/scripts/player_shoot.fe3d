6 47
META script_type_update
META script_execution_waiting

/// Commonly used values - constants
CONST STR skelID = "player_skeleton"
CONST STR gunID = misc:string_concat("player_", _equippedGunType)
CONST STR username = fe3d:client_get_username()
STR gunShootID = misc:string_concat(_equippedGunType, "_shoot")

/// Inputs
BOOL lmbPressed = fe3d:input_is_mouse_pressed("BUTTON_LEFT")
BOOL lmbDown = fe3d:input_is_mouse_down("BUTTON_LEFT")

/// Update shooting
BOOL mustFireSemi = (_gunDataSemi[_equippedGunIndex] IS <true> AND lmbPressed IS <true>)
BOOL mustFireAuto = (_gunDataSemi[_equippedGunIndex] IS <false> AND lmbDown IS <true>)
BOOL isGunShootStarted = fe3d:model_is_animation_started(gunID, gunShootID)
IF isGunShootStarted IS <false> AND _isGunReloading IS <false>:
    IF _isGunEquipped IS <true>:
        IF mustFireSemi IS <true> OR mustFireAuto IS <true>:
            /// Fire a bullet
            fe3d:model_start_animation(gunID, gunShootID, 0)
            fe3d:sound_play_forced(gunShootID, 0, 0)
            /// Send shoot message to server
            STR message = misc:string_concat("player_gun_shoot:", username)
            EDIT message = misc:string_concat(message, ":")
            EDIT message = misc:string_concat(message, _equippedGunType)
            fe3d:client_send_tcp_message(message)
