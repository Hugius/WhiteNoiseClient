36 55
META script_type_update
META script_execution_waiting

/// Constants
CONST STR skelID = "player_skeleton"
CONST STR gunID = misc:string_concat("player_", _equippedGunType)
CONST STR gunfireID = "player_gunfire"
CONST STR gunfire1ID = "player_gunfire1"
CONST STR gunfire2ID = "player_gunfire2"
CONST STR gunShootID = misc:string_concat(_equippedGunType, "_shoot")
CONST STR username = fe3d:client_get_username()
CONST BOOL lmbPressed = fe3d:input_is_mouse_pressed("BUTTON_LEFT")
CONST BOOL lmbDown = fe3d:input_is_mouse_down("BUTTON_LEFT")
CONST BOOL isSemi = _gunDataSemi[_equippedGunIndex]
CONST BOOL mustFireSemi = (isSemi IS <true> AND lmbPressed IS <true>)
CONST BOOL mustFireAuto = (isSemi IS <false> AND lmbDown IS <true>)
CONST BOOL isGunShootStarted = fe3d:model_is_animation_started(gunID, gunShootID)

/// Non-constants
STR skelAdsID = misc:string_concat("skeleton_", _equippedGunType)
EDIT skelAdsID = misc:string_concat(skelAdsID, "_ads")
BOOL isSkelAdsStarted = fe3d:model_is_animation_started(skelID, skelAdsID)
BOOL isSkelAdsPaused = <false>
IF isSkelAdsStarted IS <true>:
    EDIT isSkelAdsPaused = fe3d:model_is_animation_paused(skelID, skelAdsID)

/// Update gunfire
IF _gunfireTickCount IS _maxGunfireTickCount:
    fe3d:billboard_set_visible(gunfire1ID, <false>)
    fe3d:billboard_set_visible(gunfire2ID, <false>)
    fe3d:light_set_visible(gunfireID, <false>)
ELSE:
    INCR _gunfireTickCount 1

/// Update shooting
IF isGunShootStarted IS <false> AND _isGunReloading IS <false>:
    IF _isGunEquipped IS <true> AND _bulletCount MORE 0:
        IF isSkelAdsStarted IS <false> OR isSkelAdsPaused IS <true>:
            IF mustFireSemi IS <true> OR mustFireAuto IS <true>:
                /// Fire a bullet
                fe3d:model_start_animation(gunID, gunShootID, 0)
                fe3d:sound_play_forced(gunShootID, 0, 0)
                fe3d:billboard_set_visible(gunfire1ID, <true>)
                fe3d:billboard_set_visible(gunfire2ID, <true>)
                fe3d:light_set_visible(gunfireID, <true>)
                EDIT _gunfireTickCount = 0
                DECR _bulletCount 1
                /// Send shoot message to server
                STR message = misc:string_concat("forward:player_gun_shoot:", username)
                EDIT message = misc:string_concat(message, ":")
                EDIT message = misc:string_concat(message, _equippedGunType)
                fe3d:client_send_tcp_message(message)
