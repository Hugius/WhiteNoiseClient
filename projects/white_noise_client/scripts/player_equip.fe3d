33 52
META script_type_update
META script_execution_waiting

/// Commonly used values
CONST STR username = fe3d:client_get_username()
CONST STR skelID = "player_skeleton"
CONST STR gunID = misc:string_concat("player_", _equippedGunType)
CONST STR armsID = "skeleton_walk_arms"
STR armID = misc:string_concat("skeleton_", _equippedGunType)
EDIT armID = misc:string_concat(armID, "_eqp")
STR message = ""
BOOL isArmsStarted = fe3d:model_is_animation_started(skelID, armsID)

/// Inputs
INT scrollDir = fe3d:input_get_mousewheel_direction()
IF scrollDir NOT 0:
    EDIT _wasMouseScrolled = <true>

/// Update gun equip/unequip trigger
IF _wasMouseScrolled IS <true> AND _isGunAds IS <false> AND _isGunReloading IS <false>:
    IF _isGunEquipping IS <false> AND _isGunUnequipping IS <false>:
        IF isArmsStarted IS <false>:
            IF _isGunEquipped IS <true>:
                fe3d:model_resume_animation(skelID, armID)
                EDIT _isGunEquipped = <false>
                EDIT _isGunUnequipping = <true>
            ELSE:
                fe3d:model_set_visible(gunID, <true>)
                fe3d:model_start_animation(skelID, armID, 0)
                fe3d:model_set_animation_autopaused(skelID, armID, <true>)
                EDIT _isGunEquipping = <true>

/// Reset mouse scroll state
IF isArmsStarted IS <false> AND _isGunAds IS <false>:
    EDIT _wasMouseScrolled = <false>

/// Update gun equipping
IF _isGunEquipping IS <true>:
    BOOL isPaused = fe3d:model_is_animation_paused(skelID, armID)
    IF isPaused IS <true>:
        EDIT _gunOffsetY = _equippedGunOffsetY
        EDIT _isGunEquipping = <false>
        EDIT _isGunEquipped = <true>

/// Update gun unequipping
IF _isGunUnequipping IS <true>:
    BOOL isStarted = fe3d:model_is_animation_started(skelID, armID)
    IF isStarted IS <false>:
        EDIT _gunOffsetY = _unequippedGunOffsetY
        EDIT _isGunUnequipping = <false>
        fe3d:model_set_visible(gunID, <false>)

/// Update gun height
IF _isGunEquipping IS <true>:
    INCR _gunOffsetY _gunOffsetSpeedY
IF _isGunUnequipping IS <true>:
    DECR _gunOffsetY _gunOffsetSpeedY
