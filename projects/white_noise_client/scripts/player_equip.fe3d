2 0
META script_type_update
META script_execution_waiting

/// Commonly used values
STR primaryGunModelID = misc:string_concat("player_", _primaryGun)
STR armEquipID = misc:string_concat("skeleton_", _primaryGun)
EDIT armEquipID = misc:string_concat(armEquipID, "_eqp")

/// Update gun equipping - starting arm animation
IF _isGunEquipped IS <false> AND _isEquippingGun IS <false>:
    IF _isUnequippingGun IS <false>:
        fe3d:model_start_animation("player_skeleton", armEquipID, 0)
        fe3d:model_set_visible(primaryGunModelID, <true>)
        EDIT _isEquippingGun = <true>

/// Update gun equipping - pausing arm animation
IF _isEquippingGun IS <true>:
    BOOL isStarted = fe3d:model_is_animation_started("player_skeleton", armEquipID)
    IF isStarted IS <true>:
        BOOL isPlaying = fe3d:model_is_animation_playing("player_skeleton", armEquipID)
        INT index = fe3d:model_get_animation_frame_index("player_skeleton", armEquipID)
        IF isPlaying IS <true> AND index IS 2:
            fe3d:model_pause_animation("player_skeleton", armEquipID)
            EDIT _gunHeightOffset = 0.0
            EDIT _isEquippingGun = <false>
            EDIT _isGunEquipped = <true>

/// Update gun unequipping - finishing up
IF _isUnequippingGun IS <true>:
    BOOL isStarted = fe3d:model_is_animation_started("player_skeleton", armEquipID)
    IF isStarted IS <false>:
        fe3d:model_set_visible(primaryGunModelID, <false>)
        EDIT _gunHeightOffset = -1.0
        EDIT _isUnequippingGun = <false>
        EDIT _isGunEquipped = <false>

/// Update gun equipping - gun height
IF _isEquippingGun IS <true>:
    INCR _gunHeightOffset _gunHeightOffsetSpeed

/// Update gun unequipping - gun height
IF _isUnequippingGun IS <true>:
    DECR _gunHeightOffset _gunHeightOffsetSpeed

/// Update gun swapping - resuming arm animation
STR armsID = "skeleton_walk_arms"
BOOL isArmsPlaying = fe3d:model_is_animation_started("player_skeleton", armsID)
INT scrollDir = fe3d:input_get_mousewheel_direction()
IF scrollDir NOT 0 AND _isADS IS <false> AND _isReloading IS <false>:
    IF _isEquippingGun IS <false> AND _isUnequippingGun IS <false>:
        IF isArmsPlaying IS <false> AND _secondaryGun NOT "":
            IF _isSwappingGun IS <false>:
                fe3d:model_resume_animation("player_skeleton", armEquipID)
                EDIT _isGunEquipped = <false>
                EDIT _isUnequippingGun = <true>
                EDIT _isSwappingGun = <true>

/// Update gun swapping - finishing up
IF _isSwappingGun IS <true> AND _isUnequippingGun IS <false>:
    STR secondaryID = _secondaryGun
    INT secondaryIndex = _secondaryGunIndex
    EDIT _secondaryGun = _primaryGun
    EDIT _secondaryGunIndex = _primaryGunIndex
    EDIT _primaryGun = secondaryID
    EDIT _primaryGunIndex = secondaryIndex
    EDIT _isSwappingGun = <false>
