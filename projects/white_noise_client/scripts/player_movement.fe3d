53 21
META script_type_update
META script_execution_waiting

/// Inputs
BOOL wDown = fe3d:input_is_key_down("KEY_W")
BOOL aDown = fe3d:input_is_key_down("KEY_A")
BOOL sDown = fe3d:input_is_key_down("KEY_S")
BOOL dDown = fe3d:input_is_key_down("KEY_D")
BOOL shiftDown = fe3d:input_is_key_down("KEY_LSHIFT")
BOOL ctrlDown = fe3d:input_is_key_down("KEY_LCTRL")

/// Values
STR gunID = _primaryGunID
INT gunIndex = _primaryGunIndex

/// Revert back eye offset
DEC offset = _skeletonEyeOffset
NEG offset
fe3d:camera_follow_z(offset)

/// Update camera height
VEC3 camPos = fe3d:camera_get_position()
EDIT camPos.y = _defaultCameraHeight
INCR camPos.y _cameraHeightOffset
fe3d:camera_set_position(camPos.x, camPos.y, camPos.z)

/// Update movement speed
EDIT _isMoving = <false>
EDIT _isSprinting = <false>
MUL _movementSpeedX _gunDistanceFactor
MUL _movementSpeedZ _gunDistanceFactor
DEC movementSpeed = _maxMovementSpeed
BOOL isSprinting = <false>
IF shiftDown IS <true> AND wDown IS <true>:
    MUL movementSpeed _sprintMultiplier
    EDIT isSprinting = <true>

/// Update movement inputs
IF wDown IS <true>:
    EDIT _movementSpeedZ = movementSpeed
    EDIT _isMoving = <true>
    EDIT _isSprinting = isSprinting
IF sDown IS <true>:
    NEG movementSpeed
    EDIT _movementSpeedZ = movementSpeed
    EDIT _isMoving = <true>
    EDIT _isSprinting = isSprinting
    NEG movementSpeed
IF dDown IS <true>:
    EDIT _movementSpeedX = movementSpeed
    EDIT _isMoving = <true>
    EDIT _isSprinting = isSprinting
IF aDown IS <true>:
    NEG movementSpeed
    EDIT _movementSpeedX = movementSpeed
    EDIT _isMoving = <true>
    EDIT _isSprinting = isSprinting
    NEG movementSpeed

/// Update movement
fe3d:camera_follow_x(_movementSpeedX)
fe3d:camera_follow_z(_movementSpeedZ)

/// Update gun position & rotation
DEC yaw = fe3d:camera_get_yaw()
INCR yaw _yawOffset
VEC3 gunPos = fe3d:camera_get_position()
VEC3 gunOffset = [0.0 0.0 0.0]
EDIT gunOffset.x = _gunDistance
EDIT gunOffset.y = _gunDataHeight[gunIndex]
EDIT gunOffset.z = _gunDistance
DEC cosYaw = math:cos(yaw)
DEC sinYaw = math:sin(yaw)
MUL gunOffset.x cosYaw
INCR gunOffset.y _gunHeightOffset
MUL gunOffset.z sinYaw
INCR gunPos gunOffset
VEC3 gunRot = fe3d:model_get_rotation(gunID)
EDIT gunRot.y = fe3d:camera_get_yaw()
NEG gunRot.y
INCR gunRot.y 90.0
fe3d:model_set_position(gunID, gunPos.x, gunPos.y, gunPos.z)
fe3d:model_set_rotation(gunID, gunRot.x, gunRot.y, gunRot.z)

/// Update skeleton position & rotation
EDIT camPos = fe3d:camera_get_position()
fe3d:model_set_position("player", camPos.x, _cameraHeightOffset, camPos.z)
fe3d:model_set_rotation("player", 0.0, gunRot.y, 0.0)

/// Move camera to eye position
fe3d:camera_follow_z(_skeletonEyeOffset)
